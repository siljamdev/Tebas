console.print "Publish dotnet project"

math.equal check.0 args.length "0"
math.equal check.1 args.0 ""
bool.or check.2 check.0 check.1

if check.2 {
	console.print "The script expects a version, and none was provided"
	exit
}

template.read dotnetPath.0 "dotnetPath"

string.set def.0 args.0
console.print f"The version is {def.0}"

string.set def.1 "W/releases/"
self.append def.1 def.0

folder.exists check.0 def.1
if check.0 {
	console.ask anw.0 "That version already exists. Do you want to override it? (Y/N): "
	self.upper anw.0
	string.equal anw.1 anw.0 "Y"
	
	if! anw.1 {
		exit
	}
}

folder.create def.1

do! con.0 {
	console.print "Possible OS:"
	console.print "    win-x64, win-x86"
	console.print "    win-arm, win-arm64"
	console.print "    linux-x64, linux-arm, linux-arm64"
	console.print "    osx-x64, osx-arm64"
	console.print "    freebsd-x64, freebsd-arm, freebsd-arm64"
	console.print "    ios-arm64"
	console.print "    android-arm, android-arm64"
	console.print "    tizen-x64"
	console.print "    tvos-arm64"
	console.print "    watchos-arm64"
	
	console.ask con.1 "Which one to use? "
	
	table.contains con.0 ["win-x64","win-x86","win-arm","win-arm64","linux-x64","linux-arm","linux-arm64","osx-x64","osx-arm64","freebsd-x64","freebsd-arm","freebsd-arm64","ios-arm64","android-arm","android-arm64","tizen-x64","tvos-arm64","watchos-arm64"] con.1
}

string.set dotnet.0 f"publish -p:PublishSingleFile=true -r {con.1} -c Release --self-contained true --output releases/{def.0}"


process.run dotnetPath.0 dotnet.0

string.set pdb.0 f"W/releases/{def.0}"

folder.list files pdb.0 "*.pdb"

string.set pdb.1 "0"
math.less pdb.2 pdb.1 files.length
while pdb.2 {
	table.access pdb.3 files pdb.1
	file.delete pdb.3
	
	math.sumUp pdb.1 pdb.1
	
	math.less pdb.2 pdb.1 files.length
}

project.read zip.0 "use7zip"

if zip.0 {
	template.read zip.1 "7zipPath"
	
	string.set zip.2 f"releases/{%pn}_{def.0}_{con.1}.zip"
	
	string.set zip.3 f"./releases/{def.0}/*"
	
	string.set zip.4 f"a -tzip \"{zip.2}\" \"{zip.3}\""
	
	process.run zip.1 zip.4
}

console.print "Publishing finished!"